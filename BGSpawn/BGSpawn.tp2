BACKUP ~weidu_external/backup/BGSpawn~

SUPPORT ~http://www.shsforums.net/topic/39639-release-bgspawn-version-111/~ //AUTHOR ~Melkor Morgoth75~

VERSION ~1.3.4~

README ~%MOD_FOLDER%/docs/BGSpawnReadme.doc~

// Copied directly from BGT-Tweaks code (ascension64)
//PATCH_MACRO are_update_offset
//for addition of extended headers, this macro updates offsets of all extended headers that occur later than the extended header modified, then updates offsets stored in the variables
//Variables that need to be defined:
// -are_update_offset_increment
// -are_update_offset_datatype (can be actor, info, spawn, entrance, container, item, vertex, ambient, variable, door, explored, anim, tiled, song, rest, automap)
//Other requirements:
// -all types of %are_update_offset_datatype%_off must have been read
// -the specified %are_update_offset_datatype%_num must have been read

DEFINE_PATCH_MACRO are_update_offset BEGIN
    SPRINT are_update_offset_datatype_off "%are_update_offset_datatype%_off"
    SPRINT are_update_offset_datatype_num "%are_update_offset_datatype%_num"
    PATCH_IF (NOT (("%are_update_offset_datatype%" STRING_COMPARE_CASE "actor")=0))
        AND (("%actor_off%" > EVALUATE_BUFFER "%%are_update_offset_datatype_off%%")
        OR (("%actor_off%" = EVALUATE_BUFFER "%%are_update_offset_datatype_off%%") AND (EVALUATE_BUFFER "%%are_update_offset_datatype_num%%" = 0))) BEGIN
        WRITE_LONG 0x54 ("%actor_off%" + "%are_update_offset_increment%")
    END
    PATCH_IF (NOT (("%are_update_offset_datatype%" STRING_COMPARE_CASE "info")=0))
        AND (("%info_off%" > EVALUATE_BUFFER "%%are_update_offset_datatype_off%%")
        OR (("%info_off%" = EVALUATE_BUFFER "%%are_update_offset_datatype_off%%") AND (EVALUATE_BUFFER "%%are_update_offset_datatype_num%%" = 0))) BEGIN
        WRITE_LONG 0x5c ("%info_off%" + "%are_update_offset_increment%")
    END
    PATCH_IF (NOT (("%are_update_offset_datatype%" STRING_COMPARE_CASE "spawn")=0))
        AND (("%spawn_off%" > EVALUATE_BUFFER "%%are_update_offset_datatype_off%%")
        OR (("%spawn_off%" = EVALUATE_BUFFER "%%are_update_offset_datatype_off%%") AND (EVALUATE_BUFFER "%%are_update_offset_datatype_num%%" = 0))) BEGIN
        WRITE_LONG 0x60 ("%spawn_off%" + "%are_update_offset_increment%")
    END
    PATCH_IF (NOT (("%are_update_offset_datatype%" STRING_COMPARE_CASE "entrance")=0))
        AND (("%entrance_off%" > EVALUATE_BUFFER "%%are_update_offset_datatype_off%%")
        OR (("%entrance_off%" = EVALUATE_BUFFER "%%are_update_offset_datatype_off%%") AND (EVALUATE_BUFFER "%%are_update_offset_datatype_num%%" = 0))) BEGIN
        WRITE_LONG 0x68 ("%entrance_off%" + "%are_update_offset_increment%")
    END
    PATCH_IF (NOT (("%are_update_offset_datatype%" STRING_COMPARE_CASE "container")=0))
        AND (("%container_off%" > EVALUATE_BUFFER "%%are_update_offset_datatype_off%%")
        OR (("%container_off%" = EVALUATE_BUFFER "%%are_update_offset_datatype_off%%") AND (EVALUATE_BUFFER "%%are_update_offset_datatype_num%%" = 0))) BEGIN
        WRITE_LONG 0x70 ("%container_off%" + "%are_update_offset_increment%")
    END
    PATCH_IF (NOT (("%are_update_offset_datatype%" STRING_COMPARE_CASE "item")=0))
        AND (("%item_off%" > EVALUATE_BUFFER "%%are_update_offset_datatype_off%%")
        OR (("%item_off%" = EVALUATE_BUFFER "%%are_update_offset_datatype_off%%") AND (EVALUATE_BUFFER "%%are_update_offset_datatype_num%%" = 0))) BEGIN
        WRITE_LONG 0x78 ("%item_off%" + "%are_update_offset_increment%")
    END
    PATCH_IF (NOT (("%are_update_offset_datatype%" STRING_COMPARE_CASE "vertex")=0))
        AND (("%vertex_off%" > EVALUATE_BUFFER "%%are_update_offset_datatype_off%%")
        OR (("%vertex_off%" = EVALUATE_BUFFER "%%are_update_offset_datatype_off%%") AND (EVALUATE_BUFFER "%%are_update_offset_datatype_num%%" = 0))) BEGIN
        WRITE_LONG 0x7c ("%vertex_off%" + "%are_update_offset_increment%")
    END
    PATCH_IF (NOT (("%are_update_offset_datatype%" STRING_COMPARE_CASE "ambient")=0))
        AND (("%ambient_off%" > EVALUATE_BUFFER "%%are_update_offset_datatype_off%%")
        OR (("%ambient_off%" = EVALUATE_BUFFER "%%are_update_offset_datatype_off%%") AND (EVALUATE_BUFFER "%%are_update_offset_datatype_num%%" = 0))) BEGIN
        WRITE_LONG 0x84 ("%ambient_off%" + "%are_update_offset_increment%")
    END
    PATCH_IF (NOT (("%are_update_offset_datatype%" STRING_COMPARE_CASE "variable")=0))
        AND (("%variable_off%" > EVALUATE_BUFFER "%%are_update_offset_datatype_off%%")
        OR (("%variable_off%" = EVALUATE_BUFFER "%%are_update_offset_datatype_off%%") AND (EVALUATE_BUFFER "%%are_update_offset_datatype_num%%" = 0))) BEGIN
        WRITE_LONG 0x88 ("%variable_off%" + "%are_update_offset_increment%")
    END
    PATCH_IF (NOT (("%are_update_offset_datatype%" STRING_COMPARE_CASE "door")=0))
        AND (("%door_off%" > EVALUATE_BUFFER "%%are_update_offset_datatype_off%%")
        OR (("%door_off%" = EVALUATE_BUFFER "%%are_update_offset_datatype_off%%") AND (EVALUATE_BUFFER "%%are_update_offset_datatype_num%%" = 0))) BEGIN
        WRITE_LONG 0xa8 ("%door_off%" + "%are_update_offset_increment%")
    END
    PATCH_IF (NOT (("%are_update_offset_datatype%" STRING_COMPARE_CASE "explored")=0))
        AND (("%explored_off%" > EVALUATE_BUFFER "%%are_update_offset_datatype_off%%")
        OR (("%explored_off%" = EVALUATE_BUFFER "%%are_update_offset_datatype_off%%") AND (EVALUATE_BUFFER "%%are_update_offset_datatype_num%%" = 0))) BEGIN
        WRITE_LONG 0xa0 ("%explored_off%" + "%are_update_offset_increment%")
    END
    PATCH_IF (NOT (("%are_update_offset_datatype%" STRING_COMPARE_CASE "anim")=0))
        AND (("%anim_off%" > EVALUATE_BUFFER "%%are_update_offset_datatype_off%%")
        OR (("%anim_off%" = EVALUATE_BUFFER "%%are_update_offset_datatype_off%%") AND (EVALUATE_BUFFER "%%are_update_offset_datatype_num%%" = 0))) BEGIN
        WRITE_LONG 0xb0 ("%anim_off%" + "%are_update_offset_increment%")
    END
    PATCH_IF (NOT (("%are_update_offset_datatype%" STRING_COMPARE_CASE "tiled")=0))
        AND (("%tiled_off%" > EVALUATE_BUFFER "%%are_update_offset_datatype_off%%")
        OR (("%tiled_off%" = EVALUATE_BUFFER "%%are_update_offset_datatype_off%%") AND (EVALUATE_BUFFER "%%are_update_offset_datatype_num%%" = 0))) BEGIN
        WRITE_LONG 0xb8 ("%tiled_off%" + "%are_update_offset_increment%")
    END
    PATCH_IF (NOT (("%are_update_offset_datatype%" STRING_COMPARE_CASE "song")=0))
        AND (("%song_off%" > EVALUATE_BUFFER "%%are_update_offset_datatype_off%%")
        OR (("%song_off%" = EVALUATE_BUFFER "%%are_update_offset_datatype_off%%") AND (EVALUATE_BUFFER "%%are_update_offset_datatype_num%%" = 0))) BEGIN
        WRITE_LONG 0xbc ("%song_off%" + "%are_update_offset_increment%")
    END
    PATCH_IF (NOT (("%are_update_offset_datatype%" STRING_COMPARE_CASE "rest")=0))
        AND (("%rest_off%" > EVALUATE_BUFFER "%%are_update_offset_datatype_off%%")
        OR (("%rest_off%" = EVALUATE_BUFFER "%%are_update_offset_datatype_off%%") AND (EVALUATE_BUFFER "%%are_update_offset_datatype_num%%" = 0))) BEGIN
        WRITE_LONG 0xc0 ("%rest_off%" + "%are_update_offset_increment%")
    END
    PATCH_IF (NOT (("%are_update_offset_datatype%" STRING_COMPARE_CASE "automap")=0))
        AND (("%automap_off%" > EVALUATE_BUFFER "%%are_update_offset_datatype_off%%")
        OR (("%automap_off%" = EVALUATE_BUFFER "%%are_update_offset_datatype_off%%") AND (EVALUATE_BUFFER "%%are_update_offset_datatype_num%%" = 0))) BEGIN
        WRITE_LONG 0xc4 ("%automap_off%" + "%are_update_offset_increment%")
    END
    READ_LONG 0x54 actor_off
    READ_LONG 0x5c info_off
    READ_LONG 0x60 spawn_off
    READ_LONG 0x68 entrance_off
    READ_LONG 0x70 container_off
    READ_LONG 0x78 item_off
    READ_LONG 0x7c vertex_off
    READ_LONG 0x84 ambient_off
    READ_LONG 0x88 variable_off
    READ_LONG 0xa8 door_off
    READ_LONG 0xa0 explored_off
    READ_LONG 0xb0 anim_off
    READ_LONG 0xb8 tiled_off
    READ_LONG 0xbc song_off
    READ_LONG 0xc0 rest_off
    READ_LONG 0xc4 automap_off
END

ALWAYS

    OUTER_SPRINT MOD_FOLDER BGSpawn

    DEFINE_ACTION_FUNCTION Set-RespawnTime INT_VAR spawns = 2400 BEGIN
        // The following replaces EIGHT_HOURS value in all .baf files with time of re-spawning from users' input. Thanks ascension64! :-)
        COPY ~%MOD_FOLDER%/scripts~ ~%MOD_FOLDER%/scripts~
        REPLACE_TEXTUALLY ~\(SetGlobalTimer("[0-9]+","[0-9A-Za-z]+",\)[\(EIGHT_HOURS\)|\(2400\)]+)~ ~\1%spawns%)~
        PRINT @1150
        COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/scripts~
    END

    ACTION_IF GAME_IS ~eet~ THEN BEGIN
        ACTION_DEFINE_ARRAY fl#noconvert BEGIN setup END // List of tra files that contain ONLY strings for the WeiDU installer and NOT game content
        ACTION_DEFINE_ARRAY fl#reload BEGIN game END  // List of tra files that need to be reloaded after conversion because they were previously loaded in the LANGUAGE section
        // On EE games, convert game content tra files to utf-8 so that games don't crash when encountering international characters.
        LAF HANDLE_CHARSETS
            INT_VAR
                infer_charsets = 1
            STR_VAR
                tra_path = EVAL ~%MOD_FOLDER%/lang~
                noconvert_array = fl#noconvert
                reload_array = fl#reload
                default_language = "english"
        END
    END

    OUTER_SPRINT "spawnPerc" 100
    OUTER_SPRINT "spawnRem" 0

    ACTION_IF GAME_IS ~bgt~ BEGIN
        // areas
        OUTER_SPRINT "Undercity" "AR7223"
        OUTER_SPRINT "WSewers" "AR7324"
        OUTER_SPRINT "CentralSewers" "AR7325"
        OUTER_SPRINT "ESewers" "AR7326"
        OUTER_SPRINT "FishingVillage" "AR8300"
        OUTER_SPRINT "CloakwoodDruids" "AR8400"
        OUTER_SPRINT "CloakwoodWyverns" "AR8500"
        OUTER_SPRINT "CloakwoodMines" "AR8600"
        OUTER_SPRINT "CloakwoodMines_L3" "AR8602"
        OUTER_SPRINT "BanditCamp" "AR8700"
        OUTER_SPRINT "CloakwoodNest" "AR8800"
        OUTER_SPRINT "CloakwoodLodge" "AR7000"
        OUTER_SPRINT "Peldvale" "AR8900"
        OUTER_SPRINT "Candlekeep_Catacombs_L2" "AR6519"
        OUTER_SPRINT "LionsWay" "AR6600"
        OUTER_SPRINT "CoastWay" "AR6900"
        OUTER_SPRINT "Larswood" "AR9000"
        OUTER_SPRINT "SpiderWood" "AR9100"
        OUTER_SPRINT "ShipwrecksCoast" "AR9200"
        OUTER_SPRINT "HighHedge" "AR7100"
        OUTER_SPRINT "Temple" "AR9300"
        OUTER_SPRINT "MutaminsGarden" "AR9400"
        OUTER_SPRINT "Lighthouse" "AR9500"
        OUTER_SPRINT "RedCanyons" "AR9600"
        OUTER_SPRINT "SouthBeregostRoad" "AR9700"
        OUTER_SPRINT "Ulcaster" "AR9799"
        OUTER_SPRINT "UlcasterRuins" "AR9798"
        OUTER_SPRINT "Gullykin" "AR9900"
        OUTER_SPRINT "ArchaeologicalSite" "ARA100"
        OUTER_SPRINT "FishermansLake" "AR3100"
        OUTER_SPRINT "NorthNashkelRoad" "AR3200"
        OUTER_SPRINT "LonelyPeaks" "AR3300"
        OUTER_SPRINT "FirewineBridge" "AR3400"
        OUTER_SPRINT "BearRiver" "AR3499"
        OUTER_SPRINT "XvartVillage" "AR3498"
        OUTER_SPRINT "NashkelCarnival" "AR3800"
        OUTER_SPRINT "ValleyoftheTombs" "AR3900"
        OUTER_SPRINT "GnollStronghold" "AR4100"
        OUTER_SPRINT "DryadFalls" "AR4200"
        OUTER_SPRINT "FirewineRuins" "AR3401"
        OUTER_SPRINT "FireLeafForest" "AR4300"
        OUTER_SPRINT "NashkelMines" "AR4400"
        OUTER_SPRINT "GibberlingMountains" "AR4600"
        OUTER_SPRINT "DurlagsTower" "ARD000"
        OUTER_SPRINT "DurlagsTower_D1" "ARD011"
        OUTER_SPRINT "DurlagsTower_D4" "ARD014"
        OUTER_SPRINT "IsleofBalduranN" "ARW500"
        OUTER_SPRINT "IsleofBalduranS" "ARW000"
        OUTER_SPRINT "WerewolfCaverns" "ARW012"
        OUTER_SPRINT "Encounter_Plains1" "AR4700"
        OUTER_SPRINT "Encounter_Plains2" "AR4701"
        OUTER_SPRINT "Encounter_Canyon1" "AR4800"
        OUTER_SPRINT "Encounter_Canyon2" "AR4801"
        OUTER_SPRINT "Encounter_Cliff1" "AR4900"
        OUTER_SPRINT "Encounter_Cliff2" "AR4901"
        OUTER_SPRINT "Encounter_Grassland1" "AR5100"
        OUTER_SPRINT "Encounter_Grassland2" "AR5101"
        OUTER_SPRINT "Encounter_Forest1" "AR5300"
        OUTER_SPRINT "Encounter_Forest2" "AR5301"
        OUTER_SPRINT "Encounter_Road" "AR5400"
        // creature files
        OUTER_SPRINT "Basilisk" "BGBASILL"
        // OUTER_SPRINT "Ghast" "BGGHAST" not-in-use
        OUTER_SPRINT "Wolf" "BGWOLF"
        // OUTER_SPRINT "GreaterWolfwere" "BGWOWEGR" not-in-use


    END ELSE ACTION_IF GAME_IS ~eet~ BEGIN
        // areas
        OUTER_SPRINT "Undercity" "BG0123"
        OUTER_SPRINT "WSewers" "BG0224"
        OUTER_SPRINT "CentralSewers" "BG0225"
        OUTER_SPRINT "ESewers" "BG0226"
        OUTER_SPRINT "FishingVillage" "BG1400"
        OUTER_SPRINT "CloakwoodDruids" "BG1600"
        OUTER_SPRINT "CloakwoodWyverns" "BG1700"
        OUTER_SPRINT "CloakwoodMines" "BG1800"
        OUTER_SPRINT "CloakwoodMines_L3" "BG1802"
        OUTER_SPRINT "BanditCamp" "BG1900"
        OUTER_SPRINT "CloakwoodNest" "BG2100"
        OUTER_SPRINT "CloakwoodLodge" "BG2200"
        OUTER_SPRINT "Peldvale" "BG2400"
        OUTER_SPRINT "Candlekeep_Catacombs_L2" "BG2619"
        OUTER_SPRINT "LionsWay" "BG2700"
        OUTER_SPRINT "CoastWay" "BG2800"
        OUTER_SPRINT "Larswood" "BG2900"
        OUTER_SPRINT "SpiderWood" "BG3000"
        OUTER_SPRINT "ShipwrecksCoast" "BG3100"
        OUTER_SPRINT "HighHedge" "BG3200"
        OUTER_SPRINT "Temple" "BG3400"
        OUTER_SPRINT "MutaminsGarden" "BG3500"
        OUTER_SPRINT "Lighthouse" "BG3600"
        OUTER_SPRINT "RedCanyons" "BG3700"
        OUTER_SPRINT "SouthBeregostRoad" "BG3800"
        OUTER_SPRINT "Ulcaster" "BG3900"
        OUTER_SPRINT "UlcasterRuins" "BG3901"
        OUTER_SPRINT "Gullykin" "BG4000"
        OUTER_SPRINT "ArchaeologicalSite" "BG4100"
        OUTER_SPRINT "FishermansLake" "BG4200"
        OUTER_SPRINT "NorthNashkelRoad" "BG4300"
        OUTER_SPRINT "LonelyPeaks" "BG4400"
        OUTER_SPRINT "FirewineBridge" "BG4500"
        OUTER_SPRINT "BearRiver" "BG4600"
        OUTER_SPRINT "XvartVillage" "BG4700"
        OUTER_SPRINT "NashkelCarnival" "BG4900"
        OUTER_SPRINT "ValleyoftheTombs" "BG5000"
        OUTER_SPRINT "GnollStronghold" "BG5100"
        OUTER_SPRINT "DryadFalls" "BG5200"
        OUTER_SPRINT "FirewineRuins" "BG5201"
        OUTER_SPRINT "FireLeafForest" "BG5300"
        OUTER_SPRINT "NashkelMines" "BG5400"
        OUTER_SPRINT "GibberlingMountains" "BG5500"
        OUTER_SPRINT "DurlagsTower" "BG0500"
        OUTER_SPRINT "DurlagsTower_D1" "BG0511"
        OUTER_SPRINT "DurlagsTower_D4" "BG0514"
        OUTER_SPRINT "IsleofBalduranN" "BG1500"
        OUTER_SPRINT "IsleofBalduranS" "BG2000"
        OUTER_SPRINT "WerewolfCaverns" "BG2012"
        OUTER_SPRINT "Encounter_Plains1" "BG5600"
        OUTER_SPRINT "Encounter_Plains2" "BG5601"
        OUTER_SPRINT "Encounter_Canyon1" "BG5700"
        OUTER_SPRINT "Encounter_Canyon2" "BG5701"
        OUTER_SPRINT "Encounter_Cliff1" "BG5800"
        OUTER_SPRINT "Encounter_Cliff2" "BG5801"
        OUTER_SPRINT "Encounter_Grassland1" "BG5900"
        OUTER_SPRINT "Encounter_Grassland2" "BG5901"
        OUTER_SPRINT "Encounter_Forest1" "BG6000"
        OUTER_SPRINT "Encounter_Forest2" "BG6001"
        OUTER_SPRINT "Encounter_Road" "BG6100"
        // creature files
        OUTER_SPRINT "Basilisk" "BASILL"
        // OUTER_SPRINT "Ghast" "GHAST" not-in-use
        OUTER_SPRINT "Wolf" "WOLF"
        // OUTER_SPRINT "GreaterWolfwere" "WOLFWEGR" not-in-use
    END

    ACTION_DEFINE_ASSOCIATIVE_ARRAY file_names BEGIN
        "AR3100" => "BG4200"
        "AR3200" => "BG4300"
        "AR3300" => "BG4400"
        "AR3400" => "BG4500"
        "AR3401" => "BG5201"
        "AR3498" => "BG4700"
        "AR3499" => "BG4600"
        "AR3800" => "BG4900"
        "AR3900" => "BG5000"
        "AR4100" => "BG5100"
        "AR4200" => "BG5200"
        "AR4300" => "BG5300"
        "AR4400" => "BG5400"
        "AR4600" => "BG5500"
        "AR6519" => "BG2619"
        "AR6600" => "BG2700"
        "AR6900" => "BG2800"
        "AR7000" => "BG2200"
        "AR7100" => "BG3200"
        "AR7223" => "BG0123"
        "AR7324" => "BG0224"
        "AR7325" => "BG0225"
        "AR7326" => "BG0226"
        "AR8300" => "BG1400"
        "AR8400" => "BG1600"
        "AR8500" => "BG1700"
        "AR8600" => "BG1800"
        "AR8602" => "BG1802"
        "AR8700" => "BG1900"
        "AR8800" => "BG2100"
        "AR8900" => "BG2400"
        "AR9000" => "BG2900"
        "AR9100" => "BG3000"
        "AR9200" => "BG3100"
        "AR9300" => "BG3400"
        "AR9400" => "BG3500"
        "AR9500" => "BG3600"
        "AR9600" => "BG3700"
        "AR9700" => "BG3800"
        "AR9798" => "BG3901"
        "AR9799" => "BG3900"
        "AR9900" => "BG4000"
        "ARA100" => "BG4100"
        "ARD000" => "BG0500"
        "ARD011" => "BG0511"
        "ARD014" => "BG0514"
        "ARW000" => "BG2000"
        "ARW012" => "BG2012"
        "ARW500" => "BG1500"
    END

END

LANGUAGE "English" "english" ~BGSpawn/lang/english/setup.tra~ ~BGSpawn/lang/english/game.tra~
LANGUAGE "German [by 10th]" "german" ~BGSpawn/lang/german/setup.tra~ ~BGSpawn/lang/german/game.tra~
LANGUAGE "Italian [by Ilot]" "italian" ~BGSpawn/lang/italian/setup.tra~ ~BGSpawn/lang/italian/game.tra~
LANGUAGE "French [by Isaya]" "french" ~BGSpawn/lang/french/setup.tra~ ~BGSpawn/lang/french/game.tra~
LANGUAGE "Russian [by Prowler]" "russian" ~BGSpawn/lang/russian/setup.tra~ ~BGSpawn/lang/russian/game.tra~
LANGUAGE "Castellano [Por Lisandro]" "Spanish" ~BGSpawn/lang/Spanish/setup.tra~ ~BGSpawn/lang/Spanish/game.tra~
LANGUAGE "简体中文 [by Delmarey and Magician]" "chinese" ~BGSpawn/lang/chinese/setup.tra~ ~BGSpawn/lang/chinese/game.tra~
LANGUAGE "繁體中文 [by Delmarey and Magician]" "chineset" ~BGSpawn/lang/chineset/setup.tra~ ~BGSpawn/lang/chineset/game.tra~

/////////////////////////////////////////////////////////////////////////////////////////////////
//                                                                                             //
// Melkor Morgoth75 - BGSpawn Leveled System (thanks to ascension64 for most of the tp2 code) //
//                                                                                             //
/////////////////////////////////////////////////////////////////////////////////////////////////
BEGIN @1101 DESIGNATED 1000 LABEL "BGSpawn-Main"
    REQUIRE_PREDICATE (GAME_IS ~bgt eet~) @1

    // Handle non-fixpacked BG2
    ACTION_IF GAME_IS ~bgt~ THEN BEGIN
        APPEND ~state.ids~ ~0x00000FC0 STATE_REALLY_DEAD~ UNLESS ~STATE_REALLY_DEAD~
    END

    // For EET
    ACTION_IF GAME_IS ~eet~ BEGIN
        ACTION_PHP_EACH file_names AS bgt => eet BEGIN
        // Note: These files are renamed, not copied to override, because a) the latter would require unnecessary
        // meddling with ascension64's BGT-Tweaks code below, and b) they're not used by the game engine anyway.
        MOVE ~%MOD_FOLDER%/info/%bgt%.nfp~ ~%MOD_FOLDER%/info/%eet%.nfp~
        MOVE ~%MOD_FOLDER%/vertex/%bgt%.vtx~ ~%MOD_FOLDER%/vertex/%eet%.vtx~
        END
        // Note: We cannot juggle the random encounter area files the same way, since they need to
        // be patched with area codes that do not always match the file names (e.g. AR4701).
    END

    // Copy all new cre files
    COPY ~%MOD_FOLDER%/cre/sbearbl.CRE~ ~override~
    COPY ~%MOD_FOLDER%/cre/sbeargr.CRE~ ~override~
    COPY ~%MOD_FOLDER%/cre/sdruid3.CRE~ ~override~
    COPY ~%MOD_FOLDER%/cre/selorc01.CRE~ ~override~
    COPY ~%MOD_FOLDER%/cre/sghastgr.CRE~ ~override~
    COPY ~%MOD_FOLDER%/cre/shobsl01.CRE~ ~override~
    COPY ~%MOD_FOLDER%/cre/rogtro.CRE~ ~override~
        SAY 0x8 @1306
        SAY 0xc @1306
    COPY ~%MOD_FOLDER%/cre/rogtroxd.CRE~ ~override~
        SAY 0x8 @1306
        SAY 0xc @1306
    COPY ~%MOD_FOLDER%/cre/rogtroxd.SPL~ ~override~
    COPY ~%MOD_FOLDER%/cre/sxvart.CRE~ ~override~
        SAY 0x8 @1303
        SAY 0xc @1303
    COPY ~%MOD_FOLDER%/cre/KOBLDGD.CRE~ ~override~
        SAY 0x8 @1302
        SAY 0xc @1302
    COPY ~%MOD_FOLDER%/cre/skobchie.CRE~ ~override~
        SAY 0x8 @1301
        SAY 0xc @1301
    COPY ~%MOD_FOLDER%/cre/sdograb.CRE~ ~override~
    COPY ~%MOD_FOLDER%/cre/sanditc.CRE~ ~override~
    COPY ~%MOD_FOLDER%/cre/srevnnt.CRE~ ~override~
    COPY ~%MOD_FOLDER%/cre/ssnake01.CRE~ ~override~
    COPY ~%MOD_FOLDER%/cre/szombl01.CRE~ ~override~
    COPY ~%MOD_FOLDER%/cre/sgnoll.CRE~ ~override~
        SAY 0x8 @1304
        SAY 0xc @1304
    COPY ~%MOD_FOLDER%/cre/sironarc.CRE~ ~override~
        SAY 0x8 @1305
        SAY 0xc @1305
    // Clone Amnish Soldier (BG1) to prevent Emerson blocking access to the mines when BGSpawn spawnees are killed by monsters
    COPY_EXISTING ~amnis3.CRE~ ~override/samnis3.CRE~
        WRITE_ASCII 0x280 ~~ #20

    ACTION_DEFINE_ASSOCIATIVE_ARRAY random_treasures_trilogy BEGIN
	    "RNDEQU01"	=>	"RNDEQU04" //RNDEQUIP.2DA - LOW
	    "RNDEQU02"	=>	"RNDEQU05" //RNDEQUIP.2DA - HIGH
	    "RNDEQU03"	=>	"RNDEQU06" //RNDEQUIP.2DA - POOP
	    "RNDMAG01"	=>	"RNDMAG03" //RNDMAGIC.2DA - LOW
	    "RNDMAG02"	=>	"RNDMAG04" //RNDMAGIC.2DA - HIGH
	    "RNDMAG03"	=>	"RNDMAG05" //RNDMAGIC.2DA - POOP
	    "RNDSCR01"	=>	"RNDSCR04" //RNDSCROL.2DA - 1st
	    "RNDSCR02"	=>	"RNDSCR05" //RNDSCROL.2DA - 2nd
	    "RNDSCR03"	=>	"RNDSCR06" //RNDSCROL.2DA - 3rd
	    "RNDSCR04"	=>	"RNDSCR07" //RNDSCROL.2DA - 4th
	    "RNDTRE01"	=>	"RNDMAG06" //RNDTREAS.2DA - POOR
	    "RNDTRE02"	=>	"RNDTRE06" //RNDTREAS.2DA - AVERAGE
	    "RNDTRE03"	=>	"RNDTRE07" //RNDTREAS.2DA - WELL_OFF
	    "RNDTRE04"	=>	"RNDTRE08" //RNDTREAS.2DA - WEALTHY
	    "RNDTRE05"	=>	"RNDTRE09" //RNDTREAS.2DA - RICH
	    "RNDWEP01"	=>	"RNDWEP04" //RNDWEP.2DA - 1st
	    "RNDWEP02"	=>	"RNDWEP05" //RNDWEP.2DA - 2nd
	    "RNDWEP03"	=>	"RNDWEP06" //RNDWEP.2DA - 3rd
    END

    DEFINE_PATCH_MACRO PATCH_RANDOM_TREASURES BEGIN
    	READ_LONG  0x2bc itm_off ELSE 0
    	READ_LONG  0x2c0 itm_num ELSE 0
    	FOR (index = 0; index < itm_num; ++index) BEGIN
    		SET currentitem = itm_off + index * 0x14
    		READ_ASCII currentitem currentitem_name (8) NULL
    		PATCH_IF (VARIABLE_IS_SET currentitem_name) BEGIN
    			TO_UPPER currentitem_name
    			PATCH_IF (VARIABLE_IS_SET $random_treasures_trilogy(~%currentitem_name%~)) BEGIN
    				WRITE_ASCIIE currentitem $random_treasures_trilogy(~%currentitem_name%~) (8)
    			END
    		END
    	END
    END

    // Clone backported BG2 creatures used in BG1 areas
    // This uses a 4-character dedicated-for-cloning prefix within my namespace to prevent accidental overlaps within the S namespace
    // Graion Dilach, 2024-12-23
    ACTION_DEFINE_ASSOCIATIVE_ARRAY backport_bg2_cres BEGIN
        basgrt01  =>   zg#sbgrt    // Greater Basilisk
        catleo01  =>   zg#scleo    // Leopard
        catlim01  =>   zg#sclim    // Mountain Lion
        catlyn01  =>   zg#sclyn    // Giant Lynx
        catpan01  =>   zg#scpan    // Panther
        cattig01  =>   zg#sctig    // Wild Tiger
        deer01    =>   zg#sdeer    // Deer
        dopgre01  =>   zg#sdopg    // Greater Doppelganger
        dopple01  =>   zg#sdopp    // Doppelganger
        elehob01  =>   zg#sgoco    // Goblin Commando
        ghogr01   =>   zg#sghgr    // Greater Ghoul
        gholor01  =>   zg#sghlr    // Ghoul Lord
        gibber01  =>   zg#sgibb    // Gibberling
        gnlcap01  =>   zg#sgnoc    // Gnoll Captain
        gnleli01  =>   zg#sgnoe    // Gnoll Elite
        gnlsla01  =>   zg#sgnos    // Gnoll Slasher
        gnlvet01  =>   zg#sgnov    // Gnoll Veteran
        hellho01  =>   zg#sheho    // Hell Hound
        hgnymph   =>   zg#snymp    // Nymph
        hobarc01  =>   zg#shoba    // Hobgoblin Archer
        hobcap01  =>   zg#shobc    // Hobgoblin Captain
        hobsha01  =>   zg#shobs    // Hobgoblin Shaman
        hobwar01  =>   zg#shobw    // Hobgoblin Warrior
        hobwiz01  =>   zg#shobm    // Hobgoblin Wizard
        icgob03   =>   zg#sgob3    // Goblin
        icgob04   =>   zg#sgob4    // Goblin Captain
        jelgra01  =>   zg#sjlga    // Gray Ooze
        jelgre01  =>   zg#sjlge    // Green Slime
        jelmus01  =>   zg#sjlmu    // Mustard Jelly
        jeloch01  =>   zg#sjloc    // Ochre Jelly
        kobcap01  =>   zg#skoca    // Kobold Captain
        kobkam01  =>   zg#skoka    // Kamikaze Kobold
        kobscr01  =>   zg#skosc    // Kobold Scribe
        kobsha01  =>   zg#skosh    // Kobold Shaman
        kobwit01  =>   zg#skowi    // Kobold Witch Doctor
        mepair01  =>   zg#smeai    // Air Mephit
        mepdus01  =>   zg#smedu    // Dust Mephit
        mepfir01  =>   zg#smefi    // Fire Mephit
        mepice01  =>   zg#smeic    // Ice Mephit
        meplig01  =>   zg#smeli    // Lightning Mephit
        mepmag01  =>   zg#smema    // Magma Mephit
        mepmin01  =>   zg#smemn    // Mineral Mephit
        mepmis01  =>   zg#smems    // Mist Mephit
        mepooz01  =>   zg#smeoo    // Ooze Mephit
        mepsmo01  =>   zg#smesm    // Smoke Mephit
        mepste01  =>   zg#smest    // Steam Mephit
        mistho01  =>   zg#smiho    // Mist Horror
        moose     =>   zg#smoos    // Moose
        mummy01   =>   zg#smumm    // Mummy
        nevm1     =>   zg#swigh    // Wight
        orc01     =>   zg#sorc     // Orc
        orc02     =>   zg#sorca    // Orc Archer
        orc03     =>   zg#sorcp    // Orc Priest
        orc04     =>   zg#sorcm    // Orc Mage
        orc05     =>   zg#sorog    // Orog
        shadow01  =>   zg#sshad    // Shadow
        shawol01  =>   zg#sshwo    // Shade Wolf
        shawol02  =>   zg#sshgw    // Greater Shade Wolf
        sickrat   =>   zg#sdrat    // Diseased Rat
        sirine01  =>   zg#ssiri    // Sirine
        skelwa    =>   zg#sskw1    // Skeleton Warrior (explicitly BG2 variant!)
        skelwa03  =>   zg#sskw3    // Skeleton Warrior (explicitly BG2 variant!)
        trolde01  =>   zg#strde    // Desert Troll
        troll01   =>   zg#strol    // Troll
        vampif01  =>   zg#svamf    // Vampire (Female)
        vampim01  =>   zg#svamm    // Vampire (Male)
        wolf01    =>   zg#swolf    // Wolf
        wolfdr01  =>   zg#swodr    // Dread Wolf
        wolfgr01  =>   zg#swogr    // Greater Wolfwere
        wolfva01  =>   zg#swova    // Vampiric Wolf
        wolfzo01  =>   zg#swozo    // Zombie Wolf
        wraith01  =>   zg#swrai    // Wraith
    END

    // Clear scriptname upon clones to prevent conflicts
    ACTION_PHP_EACH backport_bg2_cres AS crekey => crevalue BEGIN
        COPY_EXISTING ~%crekey%.cre~ ~override/%crevalue%.cre~
                WRITE_ASCII 0x280 ~~ #20
                LPM PATCH_RANDOM_TREASURES
    END

    // Individual backport fixes:
    // hobgoblin group
    // captain: restore script name to ensure wizard fleeing on death
    //          (to-do: revisit this after SCS BCfH reminds captains to use their scroll)
    COPY_EXISTING ~zg#shobc.cre~ ~override~
        WRITE_ASCII 0x280 ~HobgoblinLeader~ #20
    // shaman: split off the hobgoblin-specific block from HGSHAM01.BCS and use a common script
    //         along to prevent mods overwriting HGSHAM01 overbuffing it here, while still
    //         maintain the option for SCS to regenerate the spellbook
    //         this drops Sanctuary from the shaman though, due to lack of a common script
    //         featuring it's complete spellbook
    COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/cre/zg#shobs.baf~
    COPY_EXISTING ~zg#shobs.cre~ ~override~
        WRITE_ASCII 0x258 ~ZG#SHOBS~ #8
        WRITE_ASCII 0x260 ~PRIES6B~ #8
    // wizard: split off the hobgoblin-specific block from HGWIZ01.BCS and use a common script
    //         along to prevent mods overwriting HGWIZ01 overbuffing it here, while still
    //         maintain the option for SCS to regenerate the spellbook
    //         also cheats in the Captain's haste as a free spell (to-do: revisit after there
    //         is a guideline for mods to interact with SCS spellchoices.2da)
    COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/cre/zg#shobm.baf~
    COPY_EXISTING ~zg#shobm.cre~ ~override~
        WRITE_ASCII 0x258 ~ZG#SHOBM~ #8
        WRITE_ASCII 0x260 ~MAGE10A~ #8
    // kobold scribe: set level to a more sensible value of 7 (to allow SCS to roll a level 4 spell)
    //                this monster has a spellbook and a matching script for level 5 spells
    //                it also awards 700 XP and drops 8 random scrolls, so based on it's
    //                other stats excluding it's level, it should be around level 9
    //                but SCS sees it as level 1(!) and voids the whole spellbook
    //                it's unused in oBG2, which is why this doesn't affect the base game
    //                it's only used in high-level BGSpawn encounters
    COPY_EXISTING ~zg#skosc.cre~ ~override~
        WRITE_BYTE 0x234 7
    // mummy: delete BG2 dialog file
    //        (still uses BG2 scripts/monsters to interact with Resurrection though)
    COPY_EXISTING ~zg#smumm.cre~ ~override~
        WRITE_ASCII 0x2CC ~~ #8
    // orc priest: decrease level from 12 to 6 (it can be rolled against level 4-5 groups)
    //             so SCS only rolls a spellbook with level 4 spells
    COPY_EXISTING ~zg#sorcp.cre~ ~override~
        WRITE_BYTE 0x234 6
    // orc mage: decrease level from 12 to 7 (it can be rolled against level 4-5 groups)
    //             so SCS rolls a spellbook with a single level 4 spell
    COPY_EXISTING ~zg#sorcm.cre~ ~override~
        WRITE_BYTE 0x234 7
    // greater shade wolf: delete override script referencing BG2 area
    COPY_EXISTING ~zg#sshgw.cre~ ~override~
        WRITE_ASCII 0x248 ~~ #8
    // END of backporting

    // Compiling Scripts for Creatures
    COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/cre/sgnoll.baf~
    COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/cre/rogtro.baf~
    COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/cre/rogtroxd.baf~
    // END of Scripts for CRE

    // Copied directly from BGT-Tweaks code (ascension64)
    ACTION_IF NOT FILE_EXISTS_IN_GAME ~FW01230.BCS~ THEN BEGIN // Check if BGT-Tutu levelled spawn is installed
        COPY ~%MOD_FOLDER%/info~ ~%MOD_FOLDER%/info~
        SPRINT source "%SOURCE_RES%"
        SET info_vertex_num_new_total = 0
        PATCH_IF NOT ("%SOURCE_SIZE%" = 0) BEGIN
            FOR (i=0;i<10000;i+=1) BEGIN
            READ_ASCII ("%i%"*0xc4) info_data_new (196)
            READ_ASCII (("%i%" + 1)*0xc4) buffer ELSE 10000 (196)
            READ_SHORT ("%i%"*0xc4 + 0x2a) info_vertex_num_new
            SET info_vertex_num_new_total += "%info_vertex_num_new%"
            INNER_ACTION BEGIN
                COPY_EXISTING ~%source%.ARE~ ~override/%source%.ARE~
                READ_LONG 0x54 actor_off
                READ_LONG 0x5c info_off
                READ_LONG 0x60 spawn_off
                READ_LONG 0x68 entrance_off
                READ_LONG 0x70 container_off
                READ_LONG 0x78 item_off
                READ_LONG 0x7c vertex_off
                READ_LONG 0x84 ambient_off
                READ_LONG 0x88 variable_off
                READ_LONG 0xa8 door_off
                READ_LONG 0xa0 explored_off
                READ_LONG 0xb0 anim_off
                READ_LONG 0xb8 tiled_off
                READ_LONG 0xbc song_off
                READ_LONG 0xc0 rest_off
                READ_LONG 0xc4 automap_off
                READ_SHORT 0x5a info_num
                READ_SHORT 0x64 spawn_num
                READ_SHORT 0x74 container_num
                READ_SHORT 0x80 vertex_num
                READ_SHORT 0xa4 door_num
                FOR (n=0;n<"%spawn_num%";n+=1) BEGIN
                    WRITE_SHORT ("%spawn_off%" + "%n%"*0xc8 + 0x86) 0
                END
                SPRINT are_update_offset_datatype "info"
                SET are_update_offset_increment = 196
                LAUNCH_PATCH_MACRO are_update_offset
                INSERT_BYTES "%info_off%" 196
                WRITE_EVALUATED_ASCII "%info_off%" "%info_data_new%" #196
                WRITE_LONG ("%info_off%" + 0x2c) 0 //set first vertex index to zero
                SET info_num +=1
                WRITE_SHORT 0x5a "%info_num%"
                SPRINT are_update_offset_datatype "vertex"
                SET are_update_offset_increment = 0x4 * "%info_vertex_num_new%"
                LAUNCH_PATCH_MACRO are_update_offset
                FOR (j=0;j<"%info_vertex_num_new%";j+=1) BEGIN
                    INNER_ACTION BEGIN
                    COPY ~%MOD_FOLDER%/vertex/%source%.vtx~ ~%MOD_FOLDER%/vertex/%source%.vtx~
                        READ_LONG (("%j%" + "%info_vertex_num_new_total%" - "%info_vertex_num_new%")*0x4) vertex_data_new
                    BUT_ONLY_IF_IT_CHANGES
                    END
                    READ_SHORT 0x80 vertex_num
                    INSERT_BYTES ("%vertex_off%") 4
                    WRITE_LONG ("%vertex_off%") "%vertex_data_new%"
                    SET vertex_num +=1
                    WRITE_SHORT 0x80 "%vertex_num%"
                END
                FOR (k=1;k<"%info_num%";k+=1) BEGIN
                    READ_SHORT ("%info_off%" + "%k%"*0xc4 + 0x2c) info_vertex_first_idx
                    WRITE_SHORT ("%info_off%" + "%k%"*0xc4 + 0x2c) ("%info_vertex_first_idx%" + "%info_vertex_num_new%")
                END
                FOR (l=0;l<"%door_num%";l+=1) BEGIN
                    READ_LONG ("%door_off%" + "%l%"*0xc8 + 0x2c) door_vertex_open_first_idx
                    READ_LONG ("%door_off%" + "%l%"*0xc8 + 0x34) door_vertex_closed_first_idx
                    READ_LONG ("%door_off%" + "%l%"*0xc8 + 0x48) door_vertex_impede_closed_first_idx
                    READ_LONG ("%door_off%" + "%l%"*0xc8 + 0x50) door_vertex_impede_open_first_idx
                    WRITE_LONG ("%door_off%" + "%l%"*0xc8 + 0x2c) ("%door_vertex_open_first_idx%" + "%info_vertex_num_new%")
                    WRITE_LONG ("%door_off%" + "%l%"*0xc8 + 0x34) ("%door_vertex_closed_first_idx%" + "%info_vertex_num_new%")
                    WRITE_LONG ("%door_off%" + "%l%"*0xc8 + 0x48) ("%door_vertex_impede_closed_first_idx%" + "%info_vertex_num_new%")
                    WRITE_LONG ("%door_off%" + "%l%"*0xc8 + 0x50) ("%door_vertex_impede_open_first_idx%" + "%info_vertex_num_new%")
                END
                FOR (m=0;m<"%container_num%";m+=1) BEGIN
                    READ_LONG ("%container_off%" + "%m%"*0xc0 + 0x50) container_vertex_first_idx
                    WRITE_LONG ("%container_off%" + "%m%"*0xc0 + 0x50) ("%container_vertex_first_idx%" + "%info_vertex_num_new%")
                END
                BUT_ONLY_IF_IT_CHANGES
            END
            PATCH_IF (("%buffer%" STRING_COMPARE_CASE "10000")=0) BEGIN
                SET i = 10000
            END
            END
        END
        BUT_ONLY_IF_IT_CHANGES
    END
    PRINT @1150
    COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/scripts~
    // Housekeeping for EET
    ACTION_IF GAME_IS ~eet~ BEGIN
        ACTION_PHP_EACH file_names AS bgt => eet BEGIN
            MOVE ~%MOD_FOLDER%/info/%eet%.nfp~ ~%MOD_FOLDER%/info/%bgt%.nfp~
            MOVE ~%MOD_FOLDER%/vertex/%eet%.vtx~ ~%MOD_FOLDER%/vertex/%bgt%.vtx~
        END
    END

BEGIN @1161 SUBCOMPONENT @1160 DESIGNATED 2100 LABEL "BGSpawn-RespawnTime8Hours"
    REQUIRE_PREDICATE MOD_IS_INSTALLED "BGSpawn.tp2" ( ID_OF_LABEL "BGSpawn.tp2" "BGSpawn-Main" ) @2
    OUTER_SET spawns = 2400 // 8 Hours
    LAUNCH_ACTION_FUNCTION Set-RespawnTime INT_VAR spawns END

BEGIN @1162 SUBCOMPONENT @1160 DESIGNATED 2200 LABEL "BGSpawn-RespawnTime1Day"
    REQUIRE_PREDICATE MOD_IS_INSTALLED "BGSpawn.tp2" ( ID_OF_LABEL "BGSpawn.tp2" "BGSpawn-Main" ) @2
    OUTER_SET spawns = 7200 // 1 Day
    LAUNCH_ACTION_FUNCTION Set-RespawnTime INT_VAR spawns END

BEGIN @1163 SUBCOMPONENT @1160 DESIGNATED 2300 LABEL "BGSpawn-RespawnTime10Days"
    REQUIRE_PREDICATE MOD_IS_INSTALLED "BGSpawn.tp2" ( ID_OF_LABEL "BGSpawn.tp2" "BGSpawn-Main" ) @2
    OUTER_SET spawns = 72000 // 10 Days
    LAUNCH_ACTION_FUNCTION Set-RespawnTime INT_VAR spawns END

BEGIN @1164 SUBCOMPONENT @1160 DESIGNATED 2400 LABEL "BGSpawn-RespawnTime50DaysNone"
    REQUIRE_PREDICATE MOD_IS_INSTALLED "BGSpawn.tp2" ( ID_OF_LABEL "BGSpawn.tp2" "BGSpawn-Main" ) @2
    OUTER_SET spawns = 360000 // 50 Days
    LAUNCH_ACTION_FUNCTION Set-RespawnTime INT_VAR spawns END

////////////////////////////////////
//                                //
// Probability of spawn point     //
// (with a nod to bgeespawn 0.3!) //
//                                //
////////////////////////////////////
BEGIN @1501 SUBCOMPONENT @1500 DESIGNATED 3100 LABEL "BGSpawn-SpawnProbability-15"
    REQUIRE_PREDICATE MOD_IS_INSTALLED "BGSpawn.tp2" ( ID_OF_LABEL "BGSpawn.tp2" "BGSpawn-Main" ) @2
    OUTER_SPRINT "spawnPerc" 15
    OUTER_SPRINT "spawnRem" 85
    COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/scripts~
BEGIN @1502 SUBCOMPONENT @1500 DESIGNATED 3200 LABEL "BGSpawn-SpawnProbability-25"
    REQUIRE_PREDICATE MOD_IS_INSTALLED "BGSpawn.tp2" ( ID_OF_LABEL "BGSpawn.tp2" "BGSpawn-Main" ) @2
    OUTER_SPRINT "spawnPerc" 25
    OUTER_SPRINT "spawnRem" 75
    COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/scripts~
BEGIN @1503 SUBCOMPONENT @1500 DESIGNATED 3300 LABEL "BGSpawn-SpawnProbability-33"
    REQUIRE_PREDICATE MOD_IS_INSTALLED "BGSpawn.tp2" ( ID_OF_LABEL "BGSpawn.tp2" "BGSpawn-Main" ) @2
    OUTER_SPRINT "spawnPerc" 33
    OUTER_SPRINT "spawnRem" 67
    COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/scripts~
BEGIN @1504 SUBCOMPONENT @1500 DESIGNATED 3400 LABEL "BGSpawn-SpawnProbability-50"
    REQUIRE_PREDICATE MOD_IS_INSTALLED "BGSpawn.tp2" ( ID_OF_LABEL "BGSpawn.tp2" "BGSpawn-Main" ) @2
    OUTER_SPRINT "spawnPerc" 50
    OUTER_SPRINT "spawnRem" 50
    COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/scripts~
BEGIN @1505 SUBCOMPONENT @1500 DESIGNATED 3500 LABEL "BGSpawn-SpawnProbability-66"
    REQUIRE_PREDICATE MOD_IS_INSTALLED "BGSpawn.tp2" ( ID_OF_LABEL "BGSpawn.tp2" "BGSpawn-Main" ) @2
    OUTER_SPRINT "spawnPerc" 66
    OUTER_SPRINT "spawnRem" 34
    COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/scripts~
BEGIN @1506 SUBCOMPONENT @1500 DESIGNATED 3600 LABEL "BGSpawn-SpawnProbability-75"
    REQUIRE_PREDICATE MOD_IS_INSTALLED "BGSpawn.tp2" ( ID_OF_LABEL "BGSpawn.tp2" "BGSpawn-Main" ) @2
    OUTER_SPRINT "spawnPerc" 75
    OUTER_SPRINT "spawnRem" 25
    COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/scripts~
BEGIN @1507 SUBCOMPONENT @1500 DESIGNATED 3700 LABEL "BGSpawn-SpawnProbability-85"
    REQUIRE_PREDICATE MOD_IS_INSTALLED "BGSpawn.tp2" ( ID_OF_LABEL "BGSpawn.tp2" "BGSpawn-Main" ) @2
    OUTER_SPRINT "spawnPerc" 85
    OUTER_SPRINT "spawnRem" 15
    COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/scripts~
BEGIN @1508 SUBCOMPONENT @1500 DESIGNATED 3800 LABEL "BGSpawn-SpawnProbability-100"
    REQUIRE_PREDICATE MOD_IS_INSTALLED "BGSpawn.tp2" ( ID_OF_LABEL "BGSpawn.tp2" "BGSpawn-Main" ) @2
    OUTER_SPRINT "spawnPerc" 100
    OUTER_SPRINT "spawnRem" 0
    COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/scripts~

/////////////////////////////////////////////////////////////////////////////////////
//                                                                                 //
// Vampiric Wolf Lord (inspired to DavidW's Wolf of Ulcaster, with his permission) //
//                                                                                 //
/////////////////////////////////////////////////////////////////////////////////////
BEGIN @1200 DESIGNATED 4000 LABEL "BGSpawn-VampiricWolfLord"
    COPY ~%MOD_FOLDER%/cre/mm75hog.CRE~ ~override~ //its ~howl-of-the-grave~ spell creature
        WRITE_ASCII 0x248 ~mm75hog~
    COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/cre/mm75hog.baf~
    COPY_EXISTING ~wolfva01.cre~ ~override/wolfva01.cre~
        WRITE_ASCII 0x248 ~mm75wo~
        SAY 0x8 @1705
        SAY 0xc @1705
        WRITE_SHORT 0x24 100
        WRITE_SHORT 0x26 100
        WRITE_SHORT 0x46 ~-2~
        WRITE_BYTE 0x52 11
        WRITE_BYTE 0x53 2
        WRITE_SHORT 0x14 4000
    COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/cre/mm75wo.baf~
    COPY_EXISTING ~override/mm75wo.bcs~ ~override~
        REPLACE ~666601~ @1706
        REPLACE ~666602~ @1707
        REPLACE ~666603~ @1708
        REPLACE ~666604~ @1709
        REPLACE ~666605~ @1710

/////////////////////////////////////////////////
//                                             //
// BGSpawn for Random Encounters between Areas //
//                                             //
/////////////////////////////////////////////////
BEGIN @1105 DESIGNATED 5000 LABEL "BGSpawn-RandomEncounters"
    // Handle non-fixpacked BG2
    ACTION_IF GAME_IS ~bgt~ THEN BEGIN
        APPEND ~state.ids~ ~0x00000FC0 STATE_REALLY_DEAD~ UNLESS ~STATE_REALLY_DEAD~
    END
    // Copy area files
    ACTION_IF GAME_IS ~bgt~ BEGIN
        COPY ~%MOD_FOLDER%/are_bgt/AR4700.ARE~ ~override~
        COPY ~%MOD_FOLDER%/are_bgt/AR4701.ARE~ ~override~
        COPY ~%MOD_FOLDER%/are_bgt/AR4800.ARE~ ~override~
        COPY ~%MOD_FOLDER%/are_bgt/AR4801.ARE~ ~override~
        COPY ~%MOD_FOLDER%/are_bgt/AR4900.ARE~ ~override~
        COPY ~%MOD_FOLDER%/are_bgt/AR4901.ARE~ ~override~
        COPY ~%MOD_FOLDER%/are_bgt/AR5400.ARE~ ~override~
        COPY ~%MOD_FOLDER%/are_bgt/AR5300.ARE~ ~override~
        COPY ~%MOD_FOLDER%/are_bgt/AR5301.ARE~ ~override~
        COPY ~%MOD_FOLDER%/are_bgt/AR5100.ARE~ ~override~
        COPY ~%MOD_FOLDER%/are_bgt/AR5101.ARE~ ~override~
        COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/are_bgt~
    END ELSE ACTION_IF GAME_IS ~eet~ BEGIN
        // COPY ~%MOD_FOLDER%/are_eet/BG5600.ARE~ ~override~
        // COPY ~%MOD_FOLDER%/are_eet/BG5601.ARE~ ~override~
        // COPY ~%MOD_FOLDER%/are_eet/BG5700.ARE~ ~override~
        // COPY ~%MOD_FOLDER%/are_eet/BG5701.ARE~ ~override~
        // COPY ~%MOD_FOLDER%/are_eet/BG5800.ARE~ ~override~
        // COPY ~%MOD_FOLDER%/are_eet/BG5801.ARE~ ~override~
        // COPY ~%MOD_FOLDER%/are_eet/BG5900.ARE~ ~override~
        // COPY ~%MOD_FOLDER%/are_eet/BG5901.ARE~ ~override~
        // COPY ~%MOD_FOLDER%/are_eet/BG6000.ARE~ ~override~
        // COPY ~%MOD_FOLDER%/are_eet/BG6001.ARE~ ~override~
        // COPY ~%MOD_FOLDER%/are_eet/BG6100.ARE~ ~override~
        // COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/are_eet~

        // need to EXTEND_BOTTOM here, because COMPILE overwrite Dorn's fixed encounter (the scripts exist in the basegame already)
        EXTEND_BOTTOM ~BG5600.BCS~ ~%MOD_FOLDER%/are_eet/BG5600.baf~ EVALUATE_BUFFER
        EXTEND_BOTTOM ~BG5601.BCS~ ~%MOD_FOLDER%/are_eet/BG5601.baf~ EVALUATE_BUFFER
        EXTEND_BOTTOM ~BG5700.BCS~ ~%MOD_FOLDER%/are_eet/BG5700.baf~ EVALUATE_BUFFER
        EXTEND_BOTTOM ~BG5701.BCS~ ~%MOD_FOLDER%/are_eet/BG5701.baf~ EVALUATE_BUFFER
        EXTEND_BOTTOM ~BG5800.BCS~ ~%MOD_FOLDER%/are_eet/BG5800.baf~ EVALUATE_BUFFER
        EXTEND_BOTTOM ~BG5801.BCS~ ~%MOD_FOLDER%/are_eet/BG5801.baf~ EVALUATE_BUFFER
        EXTEND_BOTTOM ~BG5900.BCS~ ~%MOD_FOLDER%/are_eet/BG5900.baf~ EVALUATE_BUFFER
        EXTEND_BOTTOM ~BG5901.BCS~ ~%MOD_FOLDER%/are_eet/BG5901.baf~ EVALUATE_BUFFER
        EXTEND_BOTTOM ~BG6000.BCS~ ~%MOD_FOLDER%/are_eet/BG6000.baf~ EVALUATE_BUFFER
        EXTEND_BOTTOM ~BG6001.BCS~ ~%MOD_FOLDER%/are_eet/BG6001.baf~ EVALUATE_BUFFER
        EXTEND_BOTTOM ~BG6100.BCS~ ~%MOD_FOLDER%/are_eet/BG6100.baf~ EVALUATE_BUFFER
    END
    COPY ~%MOD_FOLDER%/cre/sbaloch.CRE~ ~override~
        SAY 0x8 @1307
        SAY 0xc @1307
    COPY ~%MOD_FOLDER%/cre/sftroll.CRE~ ~override~
        SAY 0x8 @1308
        SAY 0xc @1308
    COPY ~%MOD_FOLDER%/cre/sftrolxd.CRE~ ~override~
        SAY 0x8 @1308
        SAY 0xc @1308
    COPY ~%MOD_FOLDER%/cre/sftrolxd.SPL~ ~override~
    COPY ~%MOD_FOLDER%/cre/sminotau.CRE~ ~override~
    COPY ~%MOD_FOLDER%/cre/swatel01.CRE~ ~override~
        SAY 0x8 @1309
        SAY 0xc @1309
    COPY ~%MOD_FOLDER%/cre/sflamcor.CRE~ ~override~
        SAY 0x8 @1310
        SAY 0xc @1310
    COPY ~%MOD_FOLDER%/cre/sassass.CRE~ ~override~
        SAY 0x8 @1311
        SAY 0xc @1311
    COPY ~%MOD_FOLDER%/cre/sankheg.CRE~ ~override~
        SAY 0x8 @1312
        SAY 0xc @1312
    COPY ~%MOD_FOLDER%/cre/sankheg2.ITM~ ~override~

    // Compiling Scripts for Creatures
    COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/cre/sftroll.baf~
    COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/cre/sftrolxd.baf~
    // END of Scripts for CRE
